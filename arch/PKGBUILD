# Maintainer: Pat Brisbin <pbrisbin@gmail.com>
pkgname='gitsh'
pkgver=0.1
pkgrel=1
pkgdesc='An interactive shell for git'
arch=('any')
url="http://thoughtbot.github.io/${pkgname}/"
license=('custom')
depends=('ruby>=2.0')
source=("http://thoughtbot.github.io/${pkgname}/${pkgname}-${pkgver}.tar.gz")
sha1sums=('42d4fb2e4f0db4d54fdc47a61bfdfbcf4294682d')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure \
     --disable-debug \
     --disable-dependency-tracking \
     --disable-silent-rules \
     --prefix=/usr

  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install

  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/${pkgname}/LICENSE"
}

# vim:set ts=2 sw=2 et:
